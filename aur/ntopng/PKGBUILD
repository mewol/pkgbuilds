# mewol
# Maintainer: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: Ari Mizrahi <codemunchies@gmail.com>

pkgname=ntopng
pkgver=1.2.1
pkgrel=1
pkgdesc='The next generation version of the original ntop, a network traffic probe that shows the network usage'
arch=('x86_64' 'i686')
url='http://www.ntop.org/'
license=('GPL3')
options=('!makeflags')
depends=('redis' 'libxml2' 'geoip' 'libpcap' 'libnet' 'lua' 'zeromq' 'gd'
         'gdbm' 'pcre' 'rrdtool' 'psutils')
source=("http://downloads.sourceforge.net/project/ntop/ntopng/ntopng-$pkgver.tgz"
        ntopng.service
        ntopng@.service)

md5sums=('4fe64c701e6dbfa42e9ced518d9c7836'
         '132ae9af9f2fe91dc6e2fe6948d45247'
         '5d3dd12fd77756138f45769e31b67505')


build() {
  cd "$srcdir/ntopng-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr
  make geoip
  make
}

package() {
  cd "$srcdir/ntopng-$pkgver"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/ntopng"
  install -dm755 "$pkgdir/usr/share/man/man8"
  install -dm755 "$pkgdir/usr/lib/systemd/system"

  # Bin.
  install -m755 ntopng "$pkgdir/usr/share/ntopng"

  # Man
  install -m755 ntopng.8 "$pkgdir/usr/share/man/man8"

  # Docs.
  cp --no-preserve=ownership -R httpdocs scripts "$pkgdir/usr/share/ntopng"

  # Systemd service.
  install -m644 "$srcdir/ntopng.service" "$pkgdir/usr/lib/systemd/system"

  cat > "$pkgdir/usr/bin/ntopng" <<EOF
#!/bin/sh
cd /usr/share/ntopng
./ntopng "\$@"
EOF
  chmod +x "$pkgdir/usr/bin/ntopng" 
}
